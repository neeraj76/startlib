apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.2.0'
    testCompile 'junit:junit:4.12'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'com.finnirv.public'
version = '0.1.1'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'Sample Library'
            description 'A library for testing bintray upload process'
            url 'https://github.com/neeraj76/startlib'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'com.finnirv.public'
            artifactId 'startlib'
            version '0.1.1'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/neeraj76/startlib.git'
                url 'https://github.com/neeraj76/startlib'

            }
            developers {
                developer {
                    id = 'neeraj76'
                    name 'Neeraj'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.getProperty('key')
    configurations = ['archives']
    pkg {
        repo = 'generic'
        name = 'sample'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/neeraj76/startlib.git'
        publish = true
        version {
            name = '0.1.1'
            desc = 'Sample Library 0.1.1'
            released = new Date()
            vcsTag = 'v0.1.1'
        }
    }
}